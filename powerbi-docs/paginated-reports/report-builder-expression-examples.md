---
title: ตัวอย่างนิพจน์ในตัวสร้างรายงานใน Power BI
description: นิพจน์จะถูกนำมาใช้บ่อยในตัวสร้างรายงานที่มีการแบ่งหน้าของ Power BI เพื่อควบคุมเนื้อหาและลักษณะที่ปรากฏของรายงาน
ms.date: 10/21/2019
ms.service: powerbi
ms.subservice: report-builder
ms.topic: conceptual
ms.assetid: 87ddb651-a1d0-4a42-8ea9-04dea3f6afa4
author: maggiesMSFT
ms.author: maggies
ms.openlocfilehash: 48e81c91a4555b4c8ea847ddffb1413058bbb152
ms.sourcegitcommit: 7aa0136f93f88516f97ddd8031ccac5d07863b92
ms.translationtype: HT
ms.contentlocale: th-TH
ms.lasthandoff: 05/05/2020
ms.locfileid: "78921159"
---
# <a name="expression-examples-in-power-bi-report-builder"></a>ตัวอย่างนิพจน์ในตัวสร้างรายงานใน Power BI
นิพจน์จะถูกนำมาใช้บ่อยในตัวสร้างรายงานที่มีการแบ่งหน้าของ Power BI เพื่อควบคุมเนื้อหาและลักษณะที่ปรากฏของรายงาน นิพจน์จะถูกเขียนขึ้นใน Microsoft Visual Basic และสามารถใช้ฟังก์ชันที่มีอยู่ในตัว รหัสที่กำหนดเอง รายงานและตัวแปรของกลุ่ม รวมถึงตัวแปรที่ผู้ใช้กำหนด นิพจน์เริ่มต้นด้วยเครื่องหมายเท่ากับ (=)   

หัวข้อนี้ให้ตัวอย่างของนิพจน์ที่สามารถนำมาใช้ได้สำหรับงานทั่วไปในรายงาน  
  
-   [ฟังก์ชัน Visual Basic](#VisualBasicFunctions) ตัวอย่างสำหรับวันที่ สตริง การแปลงและฟังก์ชัน Visual Basic ที่เป็นเงื่อนไข  
  
-   [ฟังก์ชันรายงาน](#ReportFunctions) ตัวอย่างสำหรับค่ารวมและฟังก์ชันรายงานที่มีอยู่ในตัวอื่นๆ  
  
-   [ลักษณะที่ปรากฏของข้อมูลรายงาน](#AppearanceofReportData) ตัวอย่างสำหรับการเปลี่ยนลักษณะที่ปรากฏของรายงาน  
  
-   [คุณสมบัติ](#Properties) ตัวอย่างสำหรับการตั้งค่าคุณสมบัติหน่วยข้อมูลของรายงานในรูปแบบการควบคุมหรือการมองเห็น  
  
-   [พารามิเตอร์](#Parameters) ตัวอย่างสำหรับการใช้พารามิเตอร์ในนิพจน์  
  
-   [รหัสที่กำหนดเอง](#CustomCode) ตัวอย่างรหัสที่กำหนดเองแบบฝังตัว  
  
สำหรับข้อมูลเพิ่มเติมเกี่ยวกับนิพจน์แบบธรรมดาและแบบซับซ้อนที่คุณสามาถใช้นิพจน์ได้ และชนิดการอ้างอิงที่คุณสามารถรวมอยู่ในนิพจน์ได้ ดูที่หัวข้อภายใต้[นิพจน์ในตัวสร้างรายงานของ Power BI](report-builder-expressions.md) 
  
## <a name="functions"></a>ฟังก์ชัน  
 นิพจน์หลายตัวในรายงานมีฟังก์ชันต่าง ๆ คุณสามารถจัดรูปแบบข้อมูล นำตรรกะไปใช้และเข้าถึงรายงานเมตาดาต้าได้โดยการใช้ฟังก์ชันเหล่านี้ คุณสามารถเขียนนิพจน์ที่ใช้ฟังก์ชันจากไลบรารีเวลาเรียกใช้งานของ Microsoft Visual Basic และจาก `xref:System.Convert` และ `xref:System.Math` Namespace ได้ คุณสามารถเพิ่มการอ้างอิงไปยังฟังก์ชันในรหัสที่กำหนดเองได้ นอกจากนี้ คุณยังสามารถใช้คลาสจาก Microsoft.NET Framework รวมถึง`xref:System.Text.RegularExpressions`  
  
##  <a name="visual-basic-functions"></a><a name="VisualBasicFunctions"></a> ฟังก์ชัน Visual Basic  
 คุณสามารถใช้ฟังก์ชัน Visual Basic เพื่อจัดการข้อมูลที่แสดงในกล่องข้อความหรือที่ใช้สำหรับพารามิเตอร์ คุณสมบัติหรือพื้นที่อื่นๆ ของรายงาน ส่วนนี้มีตัวอย่างที่แสดงให้เห็นถึงฟังก์ชันบางอย่างเหล่านี้ สำหรับข้อมูลเพิ่มเติม ดูที่[ สมาชิกไลบรารีเวลาเรียกใช้งาน Visual Basic](https://go.microsoft.com/fwlink/?LinkId=198941) บน MSDN  
  
 .NET Framework มีตัวเลือกรูปแบบที่กำหนดเองหลากหลาย ตัวอย่างเช่น สำหรับรูปแบบวันที่ที่เฉพาะเจาะจง สำหรับข้อมูลเพิ่มเติม ดูที่[ชนิดของการจัดรูปแบบ](/dotnet/standard/base-types/formatting-types)  
  
### <a name="math-functions"></a>ฟังก์ชันทางคณิตศาสตร์  
  
-   ฟังก์ชัน **Round** มีประโยชน์ในการปัดตัวเลขเป็นจำนวนเต็มที่ใกล้เคียงที่สุด นิพจน์ต่อไปนี้ปัด 1.3 เป็น 1:  
  
    ```  
    = Round(1.3)  
    ```  
  
     นอกจากนี้ คุณยังสามารถเขียนนิพจน์เพื่อปัดค่าเป็นผลคูณที่คุณระบุ คล้ายกับฟังก์ชัน **MRound** ใน Excel คูณค่าโดยใช้ตัวคูณที่สร้างจำนวนเต็ม ปัดตัวเลข และจากนั้นหารโดยใช้ตัวคูณตัวเดียวกัน ตัวอย่างเช่น ในการปัด 1.3 เป็นผลคูณที่ใกล้เคียงที่สุดของ 0.2 (1.4) ให้ใช้นิพจน์ต่อไปนี้:  
  
    ```  
    = Round(1.3*5)/5  
    ```  
  
###  <a name="date-functions"></a><a name="DateFunctions"></a> ฟังก์ชันวันที่  
  
-   ฟังก์ชัน **Today** มีวันที่ในปัจจุบัน นิพจน์นี้สามารถนำไปใช้ในกล่องข้อความเพื่อแสดงวันที่ในรายงาน หรือในพารามิเตอร์เพื่อกรองข้อมูลตามวันที่ในปัจจุบัน  
  
    ```  
    =Today()  
    ```  
  
-   ใช้ฟังก์ชัน **DateInterval** ในการดึงส่วนที่เฉพาะเจาะจงของวันที่ออก ต่อไปนี้คือพารามิเตอร์ **DateInterval** ที่ถูกต้องบางตัว:

    -   DateInterval.Second
    -   DateInterval.Minute
    -   DateInterval.Hour
    -   DateInterval.Weekday
    -   DateInterval.Day
    -   DateInterval.DayOfYear
    -   DateInterval.WeekOfYear
    -   DateInterval.Month
    -   DateInterval.Quarter
    -   DateInterval.Year

    ตัวอย่างเช่น นิพจน์นี้จะแสดงหมายเลขของสัปดาห์ในปีปัจจุบันสำหรับวันที่วันนี้:
  
    ```  
    =DatePart(DateInterval.WeekOfYear, today()) 
    ```  
  
-   ฟังก์ชัน **DateAdd** มีประโยชน์สำหรับการจัดช่วงข้อมูลวันที่ตามพารามิเตอร์เดี่ยว นิพจน์ต่อไปนี้มีวันที่ระยะหกเดือนหลังวันที่จากพารามิเตอร์ที่ชื่อ *วันที่เริ่มต้น*  
  
    ```  
    =DateAdd(DateInterval.Month, 6, Parameters!StartDate.Value)  
    ```  
  
-   ฟังก์ชัน **Year** แสดงปีสำหรับวันที่ที่เฉพาะเจาะจง คุณสามารถใช้ฟังก์ชันนี้เพื่อจัดกลุ่มวันที่ร่วมกันหรือเพื่อแสดงปีตามป้ายชื่อสำหรับชุดวันที่ นิพจน์นี้มี้ปีสำหรับกลุ่มที่ระบุไว้ของวันที่บนใบสั่งขาย ฟังก์ชัน **Month** และฟังก์ชันอื่นๆ ยังสามารถนำมาใช้เพื่อจัดการวันที่ได้ สำหรับข้อมูลเพิ่มเติม ดูที่คู่มือ Visual Basic  
  
    ```  
    =Year(Fields!OrderDate.Value)  
    ```  
  
-   คุณสามารถรวมฟังก์ชันในนิพจน์เพื่อกำหนดรูปแบบได้ นิพจน์ต่อไปนี้จะเปลี่ยนแปลงรูปแบบของวันที่ในฟอร์มเดือน-วัน-ปี เป็นเดือน-สัปดาห์-หมายเลขสัปดาห์ ตัวอย่างเช่น 12/23/2009 เป็นสัปดาห์ที่ 3 ของเดือนธันวาคม:  
  
    ```  
    =Format(Fields!MyDate.Value, "MMMM") & " Week " &   
    (Int(DateDiff("d", DateSerial(Year(Fields!MyDate.Value),   
    Month(Fields!MyDate.Value),1), Fields!FullDateAlternateKey.Value)/7)+1).ToString  
    ```  
  
     เมื่อนำมาใช้เป็นเขตข้อมูลจาการคำนวณในชุดข้อมูล คุณสามารถใช้นิพจน์นี้บนแผนภูมิเพื่อรวมค่าตามสัปดาห์ภายในแต่ละเดือน  
  
-   นิพจน์ต่อไปนี้จัดรูปแบบค่า SellStartDate เป็น ดดด-ปป เขตข้อมูล SellStartDate เป็นชนิดข้อมูลวันที่เวลา  
  
    ```  
    =FORMAT(Fields!SellStartDate.Value, "MMM-yy")  
    ```  
  
-   นิพจน์ต่อไปนี้จัดรูปแบบค่าของ SellStartDate เป็น วว/ดด/ปปปป เขตข้อมูล SellStartDate เป็นชนิดข้อมูลวันที่เวลา  
  
    ```  
    =FORMAT(Fields!SellStartDate.Value, "dd/MM/yyyy")  
    ```  
  
-   ฟังก์ชัน**CDate** แปลงค่าเป็นวันที่ ฟังก์ชัน **Now** คืนค่าวันที่ที่มีวันที่และเวลาในปัจจุบันบนระบบของคุณ **DateDiff** คืนค่าระยะยาวโดยการระบุจำนวนช่วงเวลาระหว่างค่าวันที่ทั้งสอง  
  
     ตัวอย่างต่อไปนี้แสดงวันที่เริ่มต้นของปีปัจจุบัน  
  
    ```  
    =DateAdd(DateInterval.Year,DateDiff(DateInterval.Year,CDate("01/01/1900"),Now()),CDate("01/01/1900"))  
    ```  
  
-   ตัวอย่างต่อไปนี้แสดงวันที่เริ่มต้นสำหรับเดือนก่อนหน้านี้ตามเดือนปัจจุบัน  
  
    ```  
    =DateAdd(DateInterval.Month,DateDiff(DateInterval.Month,CDate("01/01/1900"),Now())-1,CDate("01/01/1900"))  
    ```  
  
-   ตัวอย่างต่อไปนี้สร้างช่วงปีระหว่าง SellStartDate และ LastReceiptDate เขตข้อมูลเหล่านี้อยู่ในชุดข้อมูลอื่นสองชุดได้แก่ DataSet1 และ DataSet2  
  
    ```  
    =DATEDIFF("yyyy", First(Fields!SellStartDate.Value, "DataSet1"), First(Fields!LastReceiptDate.Value, "DataSet2"))  
    ```  
  
-   ฟังก์ชัน **DatePart** คืนค่าจำนวนเต็มที่มีคอมโพเนนต์ที่ระบุของค่าวันที่ที่กำหนดไว้ นิพจน์ต่อไปนี้คืนค่าปีสำหรับค่าแรกของ SellStartDate ใน DataSet1 มีระบุขอบเขตชุดข้อมูลเนื่องจากมีหลายชุดข้อมูลในรายงาน  
  
    ```  
    =Datepart("yyyy", First(Fields!SellStartDate.Value, "DataSet1"))  
  
    ```  
  
-   ฟังก์ชัน **DateSerial** คืนค่าวันที่โดยการแสดงปี เดือนและวันที่ระบุ พร้อมด้วยข้อมูลเวลาที่ตั้งค่าถึงเที่ยงคืน ตัวอย่างต่อไปนี้แสดงวันที่สิ้นสุดสำหรับเดือนก่อนตามเดือนปัจจุบัน  
  
    ```  
    =DateSerial(Year(Now()), Month(Now()), "1").AddDays(-1)  
    ```  
  
-   นิพจน์ต่อไปนี้แสดงวันที่ต่าง ๆ ตามค่าพารามิเตอร์วันที่ที่ผู้ใช้เลือก  
  
|คำอธิบายตัวอย่าง|ตัวอย่าง|  
|-------------------------|-------------|  
|เมื่อวานนี้|`=DateSerial(Year(Parameters!TodaysDate.Value),Month(Parameters!TodaysDate.Value),Day(Parameters!TodaysDate.Value)-1)`|  
|สองวันที่ผ่านมา|`=DateSerial(Year(Parameters!TodaysDate.Value),Month(Parameters!TodaysDate.Value),Day(Parameters!TodaysDate.Value)-2)`|  
|หนึ่งเดือนที่ผ่านมา|`=DateSerial(Year(Parameters!TodaysDate.Value),Month(Parameters!TodaysDate.Value)-1,Day(Parameters!TodaysDate.Value))`|  
|สองเดือนที่ผ่านมา|`=DateSerial(Year(Parameters!TodaysDate.Value),Month(Parameters!TodaysDate.Value)-2,Day(Parameters!TodaysDate.Value))`|  
|หนึ่งปีที่ผ่านมา|`=DateSerial(Year(Parameters!TodaysDate.Value)-1,Month(Parameters!TodaysDate.Value),Day(Parameters!TodaysDate.Value))`|  
|สองปีที่ผ่านมา|`=DateSerial(Year(Parameters!TodaysDate.Value)-2,Month(Parameters!TodaysDate.Value),Day(Parameters!TodaysDate.Value))`|  
  
###  <a name="string-functions"></a><a name="StringFunctions"></a> ฟังก์ชันสตริง  
  
-   รวมเขตข้อมูลมากกว่าหนึ่งรายการโดยใช้ตัวดำเนินการเรียงต่อกันและค่าคงที่ของ Visual Basic นิพจน์ต่อไปนี้คืนค่าสองเขตข้อมูล แต่ละเขตข้อมูลอยู่บนบรรทัดที่แยกออกจากกันในกล่องข้อความเดียวกัน:  
  
    ```  
    =Fields!FirstName.Value & vbCrLf & Fields!LastName.Value   
    ```  
  
-   จัดรูปแบบวันที่และหมายเลขในสตริงด้วยฟังก์ชัน **Format** นิพจน์ต่อไปนี้แสดงค่าของพารามิเตอร์ *StartDate* และ *EndDate*ในรูปแบบวันที่ระยะยาว:  
  
    ```  
    =Format(Parameters!StartDate.Value, "D") & " through " &  Format(Parameters!EndDate.Value, "D")    
    ```  
  
     หากกล่องข้อความมีเพียงวันที่หรือหมายเลข คุณควรใช้คุณสมบัติจัดรูปแบบของกล่องข้อความเพื่อนำการจัดรูปแบบมาใช้แทนฟังก์ชัน **Format** ที่อยู่ภายในกล่องข้อความ  
  
-   ฟังก์ชัน **Right**, **Len** และ **InStr** มีประโยชน์สำหรับการคืนสตริงย่อย ตัวอย่างเช่น การตัดแต่ง*DOMAIN*\\*ชื่อผู้ใช้*เป็นเพียงชื่อผู้ใช้ นิพจน์ต่อไปนี้คืนสตริงบางส่วนกลับไปเป็นเครื่องหมายทับด้านขวา (\\) อักขระจากพารามิเตอร์ที่มีชื่อ *ผู้ใช้*:  
  
    ```  
    =Right(Parameters!User.Value, Len(Parameters!User.Value) - InStr(Parameters!User.Value, "\"))  
    ```  
  
     นิพจน์ต่อไปนี้ให้ผลลัพธ์เป็นค่าเดียวกันกับค่าก่อนหน้านี้ โดยใช้สมาชิกคลาส .NET Framework `xref:System.String` แทนฟังก์ชัน Visual Basic:  
  
    ```  
    =Parameters!User.Value.Substring(Parameters!User.Value.IndexOf("\")+1, Parameters!User.Value.Length-Parameters!User.Value.IndexOf("\")-1)  
    ```  
  
-   แสดงค่าที่เลือกจากพารามิเตอร์ที่มีหลายค่า นิพจน์ต่อไปนี้ใช้ฟังก์ชัน **Join**เพื่อเชื่อมค่าของพารามิเตอร์*MySelection*ที่เลือกเข้าด้วยกันเป็นสตริงเดียวที่สามารถนำมาตั้งค่าเป็นนิพจน์สำหรับค่าของกล่องข้อความในหน่วยข้อมูลของรายงาน:  
  
    ```  
    = Join(Parameters!MySelection.Value)  
    ```  
  
     ตัวอย่างต่อไปนี้เหมือนกับตัวอย่างข้างต้น อีกทั้งยังแสดงสตริงข้อความก่อนรายการของค่าที่เลือก  
  
    ```  
    ="Report for " & JOIN(Parameters!MySelection.Value, " & ")  
  
    ```  
  
-   ฟังก์ชัน **Regex** จาก .NET Framework `xref:System.Text.RegularExpressions` มีประโยชน์สำหรับการเปลี่ยนแปลงการจัดรูปแบบของสตริงที่มีอยู่ ตัวอย่างเช่น การจัดรูปแบบหมายเลขโทรศัพท์ นิพจน์ต่อไปนี้ใช้ฟังก์ชัน **Replace**ในการเปลี่ยนแปลงการจัดรูปแบบหมายเลขโทรศัพท์ 10 ตัวในเขตข้อมูลจาก "*nnn*-*nnn*-*nnnn*" เป็น "(*nnn*) *nnn*-*nnnn*":  
  
    ```  
    =System.Text.RegularExpressions.Regex.Replace(Fields!Phone.Value, "(\d{3})[ -.]*(\d{3})[ -.]*(\d{4})", "($1) $2-$3")  
    ```  
  
    > [!NOTE]  
    >  ครวจสอบว่าค่าของ Fields!Phone.Value ไม่มีพื้นที่เพิ่มเติมและเป็นชนิด `xref:System.String`  
  
### <a name="lookup"></a>ค้นหา  
  
-   คุณสามารถใช้ฟังก์ชัน **Lookup** เพื่อกู้คืนค่าจากชุดข้อมูลสำหรับความสัมพันธ์แบบหนึ่ง-ต่อ-หนึ่ง ตัวอย่างเช่น คู่ค่าหลัก โดยการระบุเขตข้อมูลหลัก นิพจน์ต่อไปนี้แสดงชื่อผลิตภัณฑ์จากชุดข้อมูล ("ผลิตภัณฑ์") ซึ่งกำหนดตัวระบุผลิตภัณฑ์เพื่อให้ตรงกับ:  
  
    ```  
    =Lookup(Fields!PID.Value, Fields!ProductID.Value, Fields.ProductName.Value, "Product")  
    ```  
  
### <a name="lookupset"></a>LookupSet  
  
-   คุณสามารถใช้ฟังก์ชัน **LookupSet** เพื่อกู้คืนชุดค่าจากชุดข้อมูลสำหรับความสัมพันธ์แบบหนึ่งต่อกลุ่ม โดยการระบุเขตข้อมูลหลัก ตัวอย่างเช่น บุคคลหนึ่งสามารถมีหมายเลขโทรศัพท์ได้หลายหมายเลข ในตัวอย่างต่อไปนี้ ถือว่าชุดข้อมูล PhoneList มีตัวระบุบุคคลและหมายเลขโทรศัพท์ในแต่ละแถว **LookupSet** คืนอาร์เรย์ของค่า นิพจน์ต่อไปนี้รวมค่าที่ส่งคืนเป็นสตริงเดียวและแสดงรายการหมายเลขโทรศัพท์สำหรับบุคคลที่ระบุโดย ContactID:  
  
    ```  
    =Join(LookupSet(Fields!ContactID.Value, Fields!PersonID.Value, Fields!PhoneNumber.Value, "PhoneList"),",")  
    ```  
  
###  <a name="conversion-functions"></a><a name="ConversionFunctions"></a> ฟังก์ชันการแปลง  
 คุณสามารถใช้ฟังก์ชัน Visual Basic ในการแปลงเขตข้อมูลจากชนิดข้อมูลหนึ่งไปเป็นชนิดข้อมูลอื่นได้ ฟังก์ชันการแปลงสามารถนำไปใช้ในการแปลงชนิดข้อมูลค่าเริ่มต้นสำหรับเขตข้อมูลไปเป็นชนิดข้อมูลที่จำเป็นสำหรับการคำนวณหรือในการรวมข้อความ  
  
-   นิพจน์ต่อไปนี้แปลงค่าคงที่ 500 เป็นชนิดทศนิยมเพื่อเปรียบเทียบกับชนิดข้อมูลการเงิน Transact-SQL ในเขตข้อมูลค่าสำหรับนิพจน์ตัวกรอง  
  
    ```  
    =CDec(500)  
    ```  
  
-   นิพจน์ต่อไปนี้แสดงจำนวนค่าที่เลือกสำหรับพารามิเตอร์ที่มีหลายค่า *MySelection*  
  
    ```  
    =CStr(Parameters!MySelection.Count)  
    ```  
  
###  <a name="decision-functions"></a><a name="DecisionFunctions"></a> ฟังก์ชันการตัดสินใจ  
  
-   ฟังก์ชัน **Iif** คืนค่าหนึ่งในสองค่า ทั้งนี้ขึ้นอยู่กับว่านิพจน์นั้นเป็นจริงหรือไม่ นิพจน์ต่อไปนี้ใช้ฟังก์ชัน **Iif** ในการคืนค่าแบบบูลีนของ **True** หากค่า`LineTotal`ไม่เกิน 100 หรือไม่ก็จะคืนค่า **False**:  
  
    ```  
    =IIF(Fields!LineTotal.Value > 100, True, False)  
    ```  
  
-   ใช้ฟังก์ชัน **IIF** หลายครั้ง (หรือที่เรียกว่า "IIF ที่ซ้อนกัน") ในการส่งคืนค่าหนึ่งในสามค่า ทั้งนี้ขึ้นอยู่กับค่าของ `PctComplete` นิพจน์ต่อไปนี้สามารถวางในสีเติมของกล่องข้อความเพื่อเปลี่ยนสีพื้นหลัง ทั้งนี้ขึ้นอยู่กับค่าในกล่องข้อความ  
  
    ```  
    =IIF(Fields!PctComplete.Value >= 10, "Green", IIF(Fields!PctComplete.Value >= 1, "Blue", "Red"))  
    ```  
  
     ค่าที่มากกว่าหรือเท่ากับ 10 จะแสดงด้วยพื้นหลังสีเขียว ค่าระหว่าง 1 กับ 9 แสดงด้วยพื้นหลังสีฟ้า และค่าที่น้อยกว่า 1 แสดงด้วยพื้นหลังสีแดง  
  
-   วิธีการอื่นในการรับฟังก์ชันเดียวกัน ให้ใช้ฟังก์ชัน **Switch** ฟังก์ชัน **Switch** มีประโยชน์เมื่อคุณมีเงื่อนไขสามอย่างหรือมากกว่านั้นในการทดสอบ ฟังก์ชัน **Switch** คืนค่าที่เกี่ยวข้องด้วยนิพจน์แรกในซีรีส์ที่ประมวลผลว่าเป็นจริง:  
  
    ```  
    =Switch(Fields!PctComplete.Value >= 10, "Green", Fields!PctComplete.Value >= 1, "Blue", Fields!PctComplete.Value = 1, "Yellow", Fields!PctComplete.Value <= 0, "Red")  
    ```  
  
     ค่าที่มากกว่าหรือเท่ากับ 10 จะแสดงด้วยพื้นหลังสีเขียว ค่าระหว่าง 1 กับ 9 แสดงด้วยพื้นหลังสีฟ้า ค่าที่เท่ากับ 1 แสดงด้วยพื้นหลังสีเหลือง และค่า 0 หรือค่าน้อยกว่านั้นแสดงด้วยพื้นหลังสีแดง  
  
-   ทดสอบค่าของเขตข้อมูล `ImportantDate` และคืนค่า "สีแดง" หากมีอายุมากกว่าหนึ่งสัปดาห์ และ "สีฟ้า" สำหรับที่เป็นอย่างอื่น นิพจน์นี้อาจสามารถใช้เพื่อควบคุมคุณสมบัติสีของกล่องข้อความในหน่วยข้อมูลของรายงาน:  
  
    ```  
    =IIF(DateDiff("d",Fields!ImportantDate.Value, Now())>7,"Red","Blue")  
    ```  
  
-   ทดสอบค่าของเขตข้อมูล `PhoneNumber` และคืนค่า "ไม่มีค่า" หากเป็น **Null**( **Nothing**ใน Visual Basic) หรือไม่ก็คืนค่าหมายเลขโทรศัพท์ นิพจน์นี้อาจสามารถใช้เพื่อควบคุมค่าของกล่องข้อความในหน่วยข้อมูลของรายงาน:  
  
    ```  
    =IIF(Fields!PhoneNumber.Value Is Nothing,"No Value",Fields!PhoneNumber.Value)  
    ```  
  
-   ทดสอบค่าของเขตข้อมูล `Department` และคืนค่าชื่อรายงานย่อยหรือ **Null**(**Nothing**ใน Visual Basic) นิพจน์นี้สามารถใช้สำหรับรายงานย่อยเข้าถึงรายละเอียดตามเงื่อนไข  
  
    ```  
    =IIF(Fields!Department.Value = "Development", "EmployeeReport", Nothing)  
    ```  
  
-   ทำการทดสอบหากค่าเขตข้อมูลเป็น Null นิพจน์นี้สามารถใช้สำหรับควบคุมคุณสมบัติ**ที่ซ่อนอยู่**ของหน่วยข้อมูลของรายงานรูป ตัวอย่างต่อไปนี้ รูปที่ระบุโดยเขตข้อมูล [LargePhoto] จะแสดงขึ้นมาก็ต่อเมื่อค่าของเขตข้อมูลไม่ใช่ Null  
  
    ```  
    =IIF(IsNothing(Fields!LargePhoto.Value),True,False)  
    ```  
  
-   ฟังก์ชัน **MonthName** คืนค่าสตริงที่มีชื่อของเดือนที่ระบุ ตัวอย่างต่อไปนี้แสดง NA ในเขตข้อมูลของเดือนเมื่อเขตข้อมูลมีค่าเป็น 0  
  
    ```  
    IIF(Fields!Month.Value=0,"NA",MonthName(IIF(Fields!Month.Value=0,1,Fields!Month.Value)))  
  
    ```  
  
##  <a name="report-functions"></a><a name="ReportFunctions"></a> ฟังก์ชันรายงาน  
 ในนิพจน์ คุณสามารถเพิ่มแหล่งอ้างอิงไปยังฟังก์ชันรายงานเพิ่มเติมที่จัดการข้อมูลในรายงาน หัวข้อนี้มีตัวอย่างสำหรับสองฟังก์ชันในฟังก์ชันเหล่านี้ 
  
###  <a name="sum"></a><a name="Sum"></a> Sum  
  
-   ฟังก์ชัน **Sum** สามารถรวมค่าในกลุ่มหรือขอบเขตข้อมูลได้ ฟังก์ชันนี้มีประโยชน์ในส่วนหัวหรือส่วนท้ายของกลุ่ม นิพจน์ต่อไปนี้แสดงผลรวมของข้อมูลในกลุ่มคำสั่งหรือขอบเขตข้อมูล:  
  
    ```  
    =Sum(Fields!LineTotal.Value, "Order")  
    ```  
  
-   นอกจากนี้ คุณยังสามารถใช้ฟังก์ชัน **Sum** สำหรับการคำนวณค่ารวมที่มีเงื่อนไข ตัวอย่างเช่น หากชุดข้อมูลมีเขตข้อมูลที่มีชื่อสถานะพร้อมด้วยค่าที่เป็นไปได้เช่น ยังไม่เริ่มต้น เริ่มต้นแล้ว เสร็จสิ้น นิพจน์ต่อไปนี้เมื่อวางลงในส่วนหัวของกลุ่ม จะทำการคำนวณผลรวมสำหรับค่าที่เสร็จสิ้นเท่านั้น:  
  
    ```  
    =Sum(IIF(Fields!State.Value = "Finished", 1, 0))  
    ```  
  
###  <a name="rownumber"></a><a name="RowNumber"></a> RowNumber  
  
-   ฟังก์ชัน **RowNumber** เมื่อนำมาใช้ในกล่องข้อความภายในขอบเขตข้อมูล จะแสดงจำนวนแถวสำหรับแต่ละอินสแตนซ์ของกล่องข้อความซึ่งนิพจน์ปรากฏขึ้น ฟังก์ชันนี้มีประโยชน์เพื่อกำหนดหมายเลขแถวในตาราง นอกจากนี้ ยังมีประโยชน์สำหรับงานที่ซับซ้อนมากขึ้น เช่น การมีตัวแบ่งหน้าตามจำนวนแถว สำหรับข้อมูลเพิ่มเติม ให้ดู [ตัวแบ่งหน้า](#PageBreaks) ในหัวข้อนี้  
  
     ขอบเขตที่คุณระบุสำหรับตัวควบคุม **RowNumber** เมื่อเริ่มต้นการเรียงลำดับใหม่ คำหลัก **Nothing** จะระบุว่าฟังก์ชันจะเริ่มต้นการนับที่แถวแรกในขอบเขตข้อมูลที่อยู่นอกสุด หากต้องการเริ่มต้นการนับภายในขอบเขตข้อมูลที่ซ้อนกัน ให้ใช้ชื่อของขอบเขตข้อมูล หากต้องการเริ่มต้นการนับภายในกลุ่ม ให้ใช้ชื่อของกลุ่ม  
  
    ```  
    =RowNumber(Nothing)  
    ```  
  
##  <a name="appearance-of-report-data"></a><a name="AppearanceofReportData"></a> ลักษณะของข้อมูลรายงาน  
 คุณสามารถใช้นิพจน์เพื่อจัดการวิธีที่ข้อมูลจะปรากฏบนรายงาน ตัวอย่างเช่น คุณสามารถแสดงค่าของสองเขตข้อมูลในกล่องข้อความเดียวได้ แสดงข้อมูลเกี่ยวกับรายงาน หรือผลกระทบจากการใส่ตัวแบ่งหน้าในรายงาน  
  
###  <a name="page-headers-and-footers"></a><a name="PageHeadersandFooters"></a> ส่วนหัวและส่วนท้ายของหน้า  
 เมื่อออกแบบรายงาน คุณอาจต้องการแสดงชื่อของรายงานและหมายเลขหน้าในส่วนท้ายของรายงาน ในการทำเช่นนี้ คุณสามารถใช้นิพจน์ต่อไปนี้ได้:  
  
-   นิพจน์ต่อไปนี้มีชื่อของรายงานและเวลาที่เรียกใช้ ซึ่งสามารถนำมาวางในกล่องข้อความในส่วนท้ายของรายงานหรือในเนื้อความของรายงานได้ เวลาถูกจัดรูปแบบด้วยสตริงการจัดรูปแบบใน .NET Framework สำหรับวันที่ระยะสั้น:  
  
    ```  
    =Globals.ReportName & ", dated " & Format(Globals.ExecutionTime, "d")  
    ```  
  
-   นิพจน์ต่อไปนี้ ซึ่งวางในกล่องข้อความในส่วนท้ายของรายงาน จะมีหมายเลขหน้าและหน้าทั้งหมดในรายงาน:  
  
    ```  
    =Globals.PageNumber & " of " & Globals.TotalPages  
    ```  
  
 ตัวอย่างต่อไปนี้อธิบายวิธีแสดงค่าแรกและค่าสุดท้ายจากหน้าในส่วนหัวของหน้า คล้ายกับที่คุณอาจพบในรายการไดเรกทอรี ตัวอย่างจะถือว่าขอบเขตข้อมูลที่มีกล่องข้อความที่มีชื่อ `LastName`  
  
-   นิพจน์ต่อไปนี้ที่วางในกล่องข้อความทางด้านซ้ายบนส่วนหัวของหน้า จะมีค่าแรกของกล่องข้อความ `LastName` บนหน้า:  
  
    ```  
    =First(ReportItems("LastName").Value)  
    ```  
  
-   นิพจน์ต่อไปนี้ที่วางในกล่องข้อความทางด้านขวาบนส่วนหัวของหน้า จะมีค่าล่าสุดของกล่องข้อความ `LastName` บนหน้า:  
  
    ```  
    =Last(ReportItems("LastName").Value)  
    ```  
  
 ตัวอย่างต่อไปนี้อธิบายวิธีการแสดงผลรวมของหน้า ตัวอย่างจะถือว่าขอบเขตข้อมูลที่มีกล่องข้อความที่มีชื่อ `Cost`  
  
-   นิพจน์ต่อไปนี้วางในส่วนหัวหรือส่วนท้ายของหน้า จะมีผลรวมของค่าในกล่องข้อความ `Cost` สำหรับหน้า:  
  
    ```  
    =Sum(ReportItems("Cost").Value)  
    ```  
  
> [!NOTE]  
>  คุณสามารถอ้างอิงถึงหน่วยข้อมูลของรายงานได้เพียงหนึ่งหน่วยต่อนิพจน์ในส่วนหัวและส่วนท้ายของหน้า นอกจากนี้ คุณยังสามารถอ้างอิงถึงชื่อกล่องข้อความได้ แต่ไม่ใช่นิพจน์ข้อมูลจริงภายในกล่องข้อความ ในนิพจน์ส่วนหัวและส่วนท้ายของหน้า  
  
###  <a name="page-breaks"></a><a name="PageBreaks"></a> ตัวแบ่งหน้า  
 ในบางรายงาน คุณอาจต้องการวางตัวแบ่งหน้าที่ตอนท้ายของจำนวนแถวที่ระบุแทนหรือรวมถึงบนกลุ่มหรือหน่วยข้อมูลของรายงาน ในการทำเช่นนี้ ให้สร้างกลุ่มที่มีกลุ่มหรือบันทึกรายละเอียดที่คุณต้องการ เพิ่มตัวแบ่งหน้าไปยังกลุ่ม และจากนั้นเพิ่มนิพจน์กลุ่มไปยังกลุ่มตามจำนวนแถวที่ระบุ  
  
-   นิพจน์ต่อไปนี้เมื่อวางในนิพจน์กลุ่ม จะกำหนดจำนวนแถว 25 แถวสำหรับแต่ละชุด เมื่อมีการกำหนดตัวแบ่งหน้าสำหรับกลุ่ม นิพจน์นี้ส่งผลให้มีตัวแบ่งหน้าทุก ๆ 25 แถว  
  
    ```  
    =Ceiling(RowNumber(Nothing)/25)  
    ```  
  
     หากต้องการอนุญาตให้ผู้ใช้สามารถตั้งค่าสำหรับจำนวนแถวแต่ละหน้า ให้สร้างพารามิเตอร์ที่มีชื่อ `RowsPerPage` และอิงนิพจน์กลุ่มตามพารามิเตอร์ ดังที่แสดงในนิพจน์ต่อไปนี้:  
  
    ```  
    =Ceiling(RowNumber(Nothing)/Parameters!RowsPerPage.Value)  
    ```  
  
##  <a name="properties"></a><a name="Properties"></a> คุณสมบัติ  
 นิพจน์ไม่เพียงแต่ใช้เพื่อแสดงข้อมูลในกล่องข้อความเท่านั้น ซึ่งอาจสามารถใช้เพื่อเปลี่ยนแปลงวิธีการนำคุณสมบัติไปใช้ในหน่วยข้อมูลของรายงานด้วย คุณสามารถเปลี่ยนแปลงข้อมูลรูปแบบสำหรับหน่วยข้อมูลของรายงาน หรือเปลี่ยนแปลงการมองเห็นของรายงานได้  
  
###  <a name="formatting"></a><a name="Formatting"></a> การจัดรูปแบบ  
  
-   นิพจน์ต่อไปนี้เมื่อนำไปใช้ในคุณสมบัติสีของกล่องข้อความ จะเปลี่ยนแปลงสีของข้อความ ทั้งนี้ขึ้นอยู่กับค่าของเขตข้อมูล `Profit` :  
  
    ```  
    =Iif(Fields!Profit.Value < 0, "Red", "Black")  
    ```  
  
     นอกจากนี้ คุณยังสามารถใช้ตัวแปรออบเจ็กต์ Visual Basic ได้ `Me` ตัวแปรนี้เป็นอีกวิธีหนึ่งในการอ้างอิงถึงค่าของกล่องข้อความ  
  
     `=Iif(Me.Value < 0, "Red", "Black")`  
  
-   นิพจน์ต่อไปนี้เมื่อนำมาใช้ในคุณสมบัติสีพื้นหลังของหน่วยข้อมูลรายงานในขอบเขตข้อมูล จะเปลี่ยนสีพื้นหลังของแต่ละแถวระหว่างสีเขียวอ่อนและสีขาว:  
  
    ```  
    =Iif(RowNumber(Nothing) Mod 2, "PaleGreen", "White")  
    ```  
  
     หากคุณใช้นิพจน์สำหรับขอบเขตที่ระบุ คุณอาจต้องระบุชุดข้อมูลสำหรับฟังก์ชันการรวม:  
  
    ```  
    =Iif(RowNumber("Employees") Mod 2, "PaleGreen", "White")  
    ```  
  
> [!NOTE]  
>  สีที่มีอยู่มาจากการแจงนับของ KnownColor .NET Framework  
  
### <a name="chart-colors"></a>แผนภูมิสี  
 ในการระบุสีสำหรับแผนภูมิรูปร่าง คุณสามารถใช้รหัสที่กำหนดเองในการควบคุมลำดับที่สีจะถูกแมปเข้ากับค่าของจุดข้อมูล สิ่งนี้จะช่วยคุณในการใช้สีที่สอดคล้องกันสำหรับแผนภูมิหลายอันที่มีกลุ่มประเภทเดียวกัน 
  
###  <a name="visibility"></a><a name="Visibility"></a> การมองเห็น  
 คุณสามารถแสดงและซ่อนหน่วยข้อมูลในรายงานโดยใช้คุณสมบัติการมองเห็นสำหรับหน่วยข้อมูลของรายงาน ในขอบเขตข้อมูล เช่น ตาราง คุณสามารถซ่อนแถวแสดงรายละเอียดในขั้นต้นได้ตามค่าในนิพจน์  
  
-   นิพจน์ต่อไปนี้เมื่อนำมาใช้สำหรับการมองเห็นเริ่มต้นของแถวรายละเอียดในกลุ่ม จะแสดงแถวรายละเอียดสำหรับยอดขายทั้งหมดไม่เกิน 90 เปอร์เซ็นต์ในเขตข้อมูล `PctQuota` :  
  
    ```  
    =Iif(Fields!PctQuota.Value>.9, False, True)  
    ```  
  
-   นิพจน์ต่อไปนี้เมื่อตั้งค่าในคุณสมบัติที่ซ่อนอยู่ของตาราง จะแสดงตารางก็ต่อเมื่อมีมากกว่า 12 แถว:  
  
    ```  
    =IIF(CountRows()>12,false,true)  
    ```  
  
-   นิพจน์ต่อไปนี้ เมื่อตั้งค่าในคุณสมบัติที่**ซ่อนอยู่**ของคอลัมน์ จะแสดงคอลัมน์ก็ต่อเมื่อมีเขตข้อมูลอยู่ในชุดข้อมูลของรายงานหลังจากข้อมูลถูกกู้คืนมาจากแหล่งข้อมูล:  
  
    ```  
    =IIF(Fields!Column_1.IsMissing, true, false)  
    ```  
  
###  <a name="urls"></a><a name="Hyperlinks"></a> URL  
 คุณสามารถกำหนด URL เองได้โดยการใช้ข้อมูลรายงาน และยังสามารถควบคุมอย่างมีเงื่อนไขว่าจะเพิ่มเป็นการดำเนินการสำหรับกล่องข้อความหรือไม่  
  
-   นิพจน์ต่อไปนี้ เมื่อนำมาใช้เป็นการดำเนินการบนกล่องข้อความ จะสร้าง URL ที่กำหนดเองซึ่งจะระบุเขตชุดข้อมูล `EmployeeID` เป็นพารามิเตอร์ URL  
  
    ```  
    ="https://adventure-works/MyInfo?ID=" & Fields!EmployeeID.Value  
    ```  
  
-   นิพจน์ต่อไปนี้ควบคุมอย่างมีเงื่อนไขว่าจะเพิ่ม URL ในกล่องข้อความหรือไม่ นิพจน์นี้ขึ้นอยู่กับพารามิเตอร์ที่มีชื่อ `IncludeURLs` ที่อนุญาตให้ผู้ใช้ตัดสินใจว่าจะรวม URL ที่ใช้งานอยู่ในรายงานหรือไม่ นิพจน์นี้จะตั้งค่าเป็นการดำเนินการในกล่องข้อความ โดยการตั้งค่าพารามิเตอร์เป็นเท็จและจากนั้นดูที่รายงาน คุณสามารถส่งรายงาน Microsoft Excel ออกไปได้โดยไม่มีไฮเปอร์ลิงก์  
  
    ```  
    =IIF(Parameters!IncludeURLs.Value,"https://adventure-works.com/productcatalog",Nothing)  
    ```  
  
##  <a name="report-data"></a><a name="ReportData"></a> ข้อมูลรายงาน  
 นิพจน์สามารถนำมาใช้ในการจัดการข้อมูลที่ใช้ในรายงาน คุณสามารถอ้างอิงถึงพารามิเตอร์และข้อมูลรายงานอื่นๆ ได้ แล้วคุณยังสามารถเปลี่ยนแปลงการสอบถามที่นำมาใช้ในการกู้คืนข้อมูลสำหรับรายงาน  
  
###  <a name="parameters"></a><a name="Parameters"></a> พารามิเตอร์  
 คุณสามารถใช้นิพจน์ในพารามิเตอร์เพื่อเปลี่ยนแปลงค่าเริ่มต้นสำหรับพารามิเตอร์ได้ ตัวอย่างเช่น คุณสามารถใช้พารามิเตอร์ในการกรองข้อมูลไปยังผู้ใช้ที่เฉพาะเจาะจงตามรหัสผู้ใช้ที่สามารถนำมาใช้ในการเรียกใช้รายงานได้  
  
-   นิพจน์ต่อไปนี้เมื่อนำมาใช้เป็นค่าเริ่มต้นสำหรับพารามิเตอร์ จะรวบรวมรหัสผู้ใช้ของบุคคลที่เรียกใช้รายงาน:  
  
    ```  
    =User!UserID  
    ```  
  
-   เพื่ออ้างอิงถึงพารามิเตอร์ในพารามิเตอร์การคิวรี ตัวกรองนิพจน์ กล่องข้อความหรือพื้นที่อื่นๆ ของรายงาน ให้ใช้คอลเลกชันส่วนกลางของ**พารามิเตอร์** ตัวอย่างนี้จะถือว่าพารามิเตอร์ถูกตั้งชื่อเป็น *แผนก*:  
  
    ```  
    =Parameters!Department.Value  
    ```  
  
-   พารามิเตอร์อาจถูกสร้างในรายงาน แต่ตั้งค่าเป็นซ่อนอยู่ เมื่อรายงานเรียกใช้บนเซิร์ฟเวอร์รายงาน พารามิเตอร์จะไม่ปรากฏแถบเครื่องมือและโปรแกรมอ่านรายงานจะไม่สามารถเปลี่ยนแปลงค่าเริ่มต้นได้ คุณสามารถใช้พารามิเตอร์ที่ซ่อนอยู่ โดยตั้งค่าเป็นค่าเริ่มต้นที่เป็นค่าคงที่ที่กำหนดเอง คุณสามารถใช้ค่านี้ในนิพจน์ใดๆ รวมถึงนิพจน์ของเขตข้อมูลได้ นิพจน์ต่อไปนี้ระบุเขตข้อมูลที่ระบุตามค่าพารามิเตอร์ค่าเริ่มต้นสำหรับพารามิเตอร์ที่มีชื่อ *ParameterField*:  
  
    ```  
    =Fields(Parameters!ParameterField.Value).Value  
    ```  
  
##  <a name="custom-code"></a><a name="CustomCode"></a> รหัสที่กำหนดเอง  
 คุณสามารถใช้รหัสที่กำหนดเองที่ฝังในรายงานได้ 
  
### <a name="using-group-variables-for-custom-aggregation"></a>การใช้ตัวแปรกลุ่มสำหรับการรวมที่กำหนดเอง  
 คุณสามารถเริ่มต้นค่าสำหรับตัวแปรกลุ่มที่อยู่ภายในขอบเขตกลุ่มเฉพาะ แล้วรวมการอ้างอิงถึงตัวแปรนั้นในนิพจน์ หนึ่งในวิธีที่คุณสามารถใช้ตัวแปรกลุ่มด้วยรหัสที่กำหนดเองได้ คือ การนำค่ารวมที่กำหนดเองไปใช้ 
  
## <a name="suppressing-null-or-zero-values-at-run-time"></a>การระงับค่า Null หรือค่าศูนย์ในเวลาที่เรียกใช้งาน  
 บางค่าในนิพจน์อาจประเมินได้เป็น Null หรือระบุไม่ได้เมื่อถึงเวลาการประมวลผลรายงาน ซึ่งอาจก่อให้เกิดข้อผิดพลาดขณะทำงานที่ส่งผลให้ **#Error** แสดงในกล่องข้อความแทนนิพจน์ที่ประเมิน ฟังก์ชัน**IIF** มีความไวต่อพฤติกรรมนี้เป็นพิเศษ เนื่องจากการเลิกชอบคำสั่ง If-Then-Else แต่ละส่วนของคำสั่ง **IIF** จะถูกประเมินผล (รวมถึงฟังก์ชันการโทร) ก่อนที่จะทำการส่งไปยังรูทีนที่ทดสอบว่า**true** หรือ **false** คำสั่ง `=IIF(Fields!Sales.Value is NOTHING, 0, Fields!Sales.Value)` สร้าง **#Error**ในรายงานที่แสดงผลหาก `Fields!Sales.Value` เป็น NOTHING  
  
 หากต้องการหลีกเลี่ยงเงื่อนไขนี้ ให้ใช้หนึ่งในกลยุทธ์ต่อไปนี้:  
  
-   ตั้งค่าตัวเศษเป็น 0 และตัวส่วนเป็น 1 หากค่าของเขตข้อมูล B เป็น 0 หรือไม่ได้ระบุ; หรือตั้งค่าตัวเศษเป็นค่าของเขตข้อมูล A และตัวส่วนเป็นค่าของเขตข้อมูล B  
  
    ```  
    =IIF(Field!B.Value=0, 0, Field!A.Value / IIF(Field!B.Value =0, 1, Field!B.Value))  
    ```  
  
-   ใช้ฟังก์ชันรหัสที่กำหนดเองในการคืนค่าสำหรับนิพจน์ ตัวอย่างต่อไปนี้คืนค่าความแตกต่างเป็นเปอร์เซ็นต์ระหว่างค่าในปัจจุบันกับค่าก่อนหน้า ซึ่งสามารถนำมาใช้ในการคำนวณค่าความแตกต่างระหว่างค่าต่อเนื่องสองค่าใดๆ และจัดการกับกรณีการเชื่อมต่อจากการเปรียบเทียบครั้งแรก (เมื่อไม่มีค่าก่อนหน้า) และกรณีไม่ว่าจะเป็นค่าก่อนหน้าหรือค่าในปัจจุบันที่เป็น **Null**(**Nothing**ใน Visual Basic)  
  
    ```  
    Public Function GetDeltaPercentage(ByVal PreviousValue, ByVal CurrentValue) As Object  
        If IsNothing(PreviousValue) OR IsNothing(CurrentValue) Then  
            Return Nothing  
        Else if PreviousValue = 0 OR CurrentValue = 0 Then  
            Return Nothing  
        Else   
            Return (CurrentValue - PreviousValue) / CurrentValue  
        End If  
    End Function  
    ```  
  
     นิพจน์ต่อไปนี้แสดงวิธีการเรียกใช้รหัสที่กำหนดเองจากกล่องข้อความ สำหรับคอนเทนเนอร์ "ColumnGroupByYear" (กลุ่มหรือขอบเขตข้อมูล)  
  
    ```  
    =Code.GetDeltaPercentage(Previous(Sum(Fields!Sales.Value),"ColumnGroupByYear"), Sum(Fields!Sales.Value))  
    ```  
  
     ซึ่งจะช่วยหลีกเลี่ยงข้อยกเว้นในเวลาที่เรียกใช้ ตอนนี้คุณสามารถใช้นิพจน์นิพจน์เช่น `=IIF(Me.Value < 0, "red", "black")` ในคุณสมบัติ **Color** ของกล่องข้อความสำหรับข้อความที่แสดงอย่างมีเงื่อนไข ขึ้นอยู่กับว่าค่ามากกว่าหรือน้อยกว่า 0 หรือไม่  
  
## <a name="next-steps"></a>ขั้นตอนถัดไป

- [รายงานแบบแบ่งหน้าใน Power BI Premium คืออะไร](paginated-reports-report-builder-power-bi.md)
  
